# LIC renderer, as a Web Worker
# Steven Ruppert
# CSCI 447 Scientific Visualization Spring 2013 -- Project 3
"use strict"

canvas = document.get-element-by-id \canvas
{width, height} = canvas
ctx = canvas.get-context \2d

ctx.fill-rect 0 0 width, height

texture = ctx.create-image-data width, height
# random alpha, RGBA pixels, bytes
for i from 3 til texture.data.length by 4
  texture.data[i] = (Math.random! * 255) | 0

image = ctx.get-image-data 0 0 width, height

# create 2D vector field
field-x = new Float32Array width * height
field-y = new Float32Array width * height
for i til height
  for j til width
    # transform to [-1, 1]
    x = j * 2 / width - 1
    y = i * 2 / height - 1

    field-x[i * height + j] = Math.sin 5 * x + y
    field-y[i * height + j] = Math.cos 2 * x + 6 * y

progress = document.get-element-by-id \progress

worker = new Worker \render-worker.js

  # render once finished
  &onmessage = !(e) ->
    if e.data.type is \log
      console.log e.data.message
    else if e.data.type is \progress
      progress.value = e.data.value
    else
      # got image
      ctx.put-image-data e.data.image, 0 0
      progress.hidden = true

  # start LIC
  &post-message {image, texture, field-x, field-y, distance: 20}

progress.hidden = false

